// Copyright 2019 the V8 project authors. All rights reserved.
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

namespace string {

extern macro SingleCharacterStringCacheConstant(): FixedArray;
extern macro UnsafeLoadFixedArrayElement(FixedArray, intptr): Object;

const kSingleCharacterStringCache: FixedArray =
    SingleCharacterStringCacheConstant();
const kMaxOneByteCharCode:
    constexpr int32 generates 'String::kMaxOneByteCharCode';

macro StringFromSingleCharCode2(char: char8|char16): String {
  const charInt = Convert<intptr>(Convert<int32>(char));
  if (Convert<int32>(char) <= kMaxOneByteCharCode) {
    const maybeString =
        UnsafeLoadFixedArrayElement(kSingleCharacterStringCache, charInt);
    if (maybeString != Undefined) {
      return UnsafeCast<String>(maybeString);
    } else
      deferred {
        const result = AllocateNonEmptySeqOneByteString(
            1, ConstantIterator(%RawDownCast<char8>(char)));
        kSingleCharacterStringCache.objects[charInt] = result;
        return result;
      }
  } else
    deferred {
      return AllocateNonEmptySeqTwoByteString(
          1, ConstantIterator(%RawDownCast<char16>(char)));
    }
}

extern macro AllocateSlicedOneByteString(uint32, String, Smi): String;
extern macro AllocateSlicedTwoByteString(uint32, String, Smi): String;

extern macro StringBuiltinsAssembler::LoadChar8(RawPtr, uintptr): char8;
extern macro StringBuiltinsAssembler::StoreChar8(RawPtr, uintptr, char8): void;

macro FastCopyStringBytes<T: type>(
    src: ConstSlice<T>, srcFrom: intptr, dest: ConstSlice<T>,
    length: uintptr): void {
  const srcPtr: RawPtr =
      src.GCUnsafeStartPointer() + torque_internal::TimesSizeOf<T>(srcFrom);
  const destPtr: RawPtr = dest.GCUnsafeStartPointer();
  const byteLength = Unsigned(torque_internal::TimesSizeOf<T>(Signed(length)));
  let i: uintptr = 0;
  while (true) {
    StoreChar8(destPtr, i, LoadChar8(srcPtr, i));
    if (++i == byteLength) break;
  }
}

macro SubString(string: String, from: uintptr, to: uintptr): String {
  const length = to - from;
  assert(length <= string.length_uintptr);
  if (length == string.length_uintptr) return string;
  if (length == 0) return kEmptyString;

  const fromInt = Convert<intptr>(from);
  try {
    StringToStringSlice(string) otherwise OneByte, TwoByte;
  } label OneByte(p: String, o: intptr, s: ConstSlice<char8>) {
    if (length == 1) return StringFromSingleCharCode2(*s.AtIndex(fromInt));
    if (length < 13) {
      const result = AllocateNonEmptySeqOneByteString(Convert<uint32>(length));
      FastCopyStringBytes(s, fromInt, &result.chars, length);
      return result;
    }
    return AllocateSlicedOneByteString(
        Convert<uint32>(length), p, SmiTag(o + fromInt));
  } label TwoByte(p: String, o: intptr, s: ConstSlice<char16>) {
    if (length < 13) {
      // TODO(pwong): Add this!
      // if (length == 1) return StringFromSingleCharCode2(*s.AtIndex(fromInt));

      const result = AllocateNonEmptySeqTwoByteString(Convert<uint32>(length));
      FastCopyStringBytes(s, fromInt, &result.chars, length);
      return result;
    }
    return AllocateSlicedTwoByteString(
        Convert<uint32>(length), p, SmiTag(o + fromInt));
  }
}

builtin SubString(implicit context: Context)(
    s: String, from: Smi, to: Smi): String {
  return SubString(
      s, Unsigned(Convert<intptr>(from)), Unsigned(Convert<intptr>(to)));
}

builtin StringSubstring(implicit context: Context)(
    s: String, from: intptr, to: intptr): String {
  return SubString(s, Unsigned(from), Unsigned(to));
}

// ES6 #sec-string.prototype.substring
transitioning javascript builtin StringPrototypeSubstring(
    js-implicit context: NativeContext, receiver: JSAny)(...arguments): String {
  // Check that {receiver} is coercible to Object and convert it to a String.
  const string: String = ToThisString(receiver, 'String.prototype.substring');
  const length: uintptr = string.length_uintptr;

  // Conversion and bounds-checks for {start}.
  const arg0 = arguments[0];
  let start: uintptr = arg0 != Undefined ? ClampToIndexRange(arg0, length) : 0;

  // Conversion and bounds-checks for {end}.
  const arg1 = arguments[1];
  let end: uintptr =
      arg1 != Undefined ? ClampToIndexRange(arg1, length) : length;
  if (end < start) {
    const tmp: uintptr = end;
    end = start;
    start = tmp;
  }
  return SubString(string, start, end);
}
}
