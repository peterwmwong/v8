// Copyright 2019 the V8 project authors. All rights reserved.
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

namespace string {

const kSlicedStringMinLength:
    constexpr int31 generates 'SlicedString::kMinLength';

extern macro SingleCharacterStringCacheConstant(): FixedArray;
const kSingleCharacterStringCache: FixedArray =
    SingleCharacterStringCacheConstant();
const kMaxOneByteCharCode:
    constexpr int31 generates 'String::kMaxOneByteCharCode';

struct AllocatedSeqString {
  string: SeqString;
  chars: RawPtr;
  byteLength: intptr;
}

macro AllocateNonEmptySeqString(
    isOneByte: bool, length: uint32): AllocatedSeqString {
  if (isOneByte) {
    const string = AllocateNonEmptySeqOneByteString(length);
    return AllocatedSeqString{
      string: string,
      chars: string.GetChars(),
      byteLength: torque_internal::TimesSizeOf<char8>(Convert<intptr>(length))
    };
  } else {
    const string = AllocateNonEmptySeqTwoByteString(length);
    return AllocatedSeqString{
      string: string,
      chars: string.GetChars(),
      byteLength: torque_internal::TimesSizeOf<char16>(Convert<intptr>(length))
    };
  }
}

macro SubStringSmallImpl(
    isOneByte: bool, srcChars: RawPtr, length: uintptr): String {
  const allocString = AllocateNonEmptySeqString(isOneByte, Convert<uint32>(length));
  let i: intptr = 0;
  while (true) {
    StoreChar8(allocString.chars, i, LoadChar8(srcChars, i));
    if (++i == allocString.byteLength) break;
  }
  return allocString.string;
}

macro SubString(string: String, from: uintptr, to: uintptr): String {
  const length = to - from;
  assert(length <= string.length_uintptr);
  if (length == string.length_uintptr) return string;
  if (length == 0) return kEmptyString;

  const from = Convert<intptr>(from);
  try {
    StringToRootStringAndOffset(string) otherwise OneByte, TwoByte,
        OneByteExternal, TwoByteExternal;
  } label OneByte(src: SeqOneByteString, srcOffset: intptr) {
    const chars =
        src.GetChars() + torque_internal::TimesSizeOf<char8>(srcOffset + from);
    goto DoSubString(src, srcOffset, chars, true);
  } label TwoByte(src: SeqTwoByteString, srcOffset: intptr) {
    const chars =
        src.GetChars() + torque_internal::TimesSizeOf<char16>(srcOffset + from);
    goto DoSubString(src, srcOffset, chars, false);
  } label OneByteExternal(src: ExternalOneByteString, srcOffset: intptr) {
    const chars =
        src.GetChars() + torque_internal::TimesSizeOf<char8>(srcOffset + from);
    goto DoSubString(src, srcOffset, chars, true);
  } label TwoByteExternal(src: ExternalTwoByteString, srcOffset: intptr) {
    const chars =
        src.GetChars() + torque_internal::TimesSizeOf<char16>(srcOffset + from);
    goto DoSubString(src, srcOffset, chars, false);
  } label DoSubString(
      src: String, srcOffset: intptr, srcChars: RawPtr, isOneByte: bool) {
    if (length >= kSlicedStringMinLength) {
      return AllocateSlicedString(
          isOneByte, Convert<uint32>(length), src, Convert<Smi>(srcOffset + from));
    }

    let isOneByte = isOneByte;
    let singleChar: intptr = kMaxOneByteCharCode + 1;
    if (length == 1) {
      singleChar = Convert<intptr>(Convert<int32>(
          isOneByte ? LoadChar8(srcChars, 0) : LoadChar16(srcChars, 0)));
      if (singleChar <= kMaxOneByteCharCode) {
        isOneByte = true;
        const cached = kSingleCharacterStringCache.objects[singleChar];
        if (cached != Undefined) return %RawDownCast<String>(cached);
      }
    }

    const result = SubStringSmallImpl(isOneByte, srcChars, length);
    if (singleChar <= kMaxOneByteCharCode)
      kSingleCharacterStringCache.objects[singleChar] = result;
    return result;
  }
}

builtin SubString(implicit context: Context)(
    s: String, from: Smi, to: Smi): String {
  return SubString(
      s, Unsigned(Convert<intptr>(from)), Unsigned(Convert<intptr>(to)));
}

builtin StringSubstring(implicit context: Context)(
    s: String, from: intptr, to: intptr): String {
  return SubString(s, Unsigned(from), Unsigned(to));
}

// ES6 #sec-string.prototype.substring
transitioning javascript builtin StringPrototypeSubstring(
    js-implicit context: NativeContext, receiver: JSAny)(...arguments): String {
  // Check that {receiver} is coercible to Object and convert it to a String.
  const string: String = ToThisString(receiver, 'String.prototype.substring');
  const length: uintptr = string.length_uintptr;

  // Conversion and bounds-checks for {start}.
  const arg0 = arguments[0];
  let start: uintptr = arg0 != Undefined ? ClampToIndexRange(arg0, length) : 0;

  // Conversion and bounds-checks for {end}.
  const arg1 = arguments[1];
  let end: uintptr =
      arg1 != Undefined ? ClampToIndexRange(arg1, length) : length;
  if (end < start) {
    const tmp: uintptr = end;
    end = start;
    start = tmp;
  }
  return SubString(string, start, end);
}
}
