// Copyright 2019 the V8 project authors. All rights reserved.
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

namespace string {

extern macro AllocateSlicedOneByteString(uint32, String, Smi): String;
extern macro AllocateSlicedTwoByteString(uint32, String, Smi): String;

extern macro StringBuiltinsAssembler::LoadChar8(RawPtr, uintptr): char8;
extern macro StringBuiltinsAssembler::StoreChar8(RawPtr, uintptr, char8): void;

const kSlicedStringMinLength:
    constexpr int31 generates 'SlicedString::kMinLength';

macro WriteToString<T: type, U: type>(
    src: ConstSlice<T>, srcFrom: intptr, dest: U, length: uintptr): U {
  assert(
      length > 0 && (src.length - srcFrom) >= Signed(length) &&
      dest.length_uintptr >= length);
  const srcPtr =
      src.GCUnsafeStartPointer() + torque_internal::TimesSizeOf<T>(srcFrom);
  const destSlice: ConstSlice<T> = &dest.chars;
  const destPtr = destSlice.GCUnsafeStartPointer();
  const byteLength = Unsigned(torque_internal::TimesSizeOf<T>(Signed(length)));
  let i: uintptr = 0;
  while (true) {
    StoreChar8(destPtr, i, LoadChar8(srcPtr, i));
    if (++i == byteLength) break;
  }
  return dest;
}

macro SubString(string: String, from: uintptr, to: uintptr): String {
  const length = to - from;
  assert(length <= string.length_uintptr);
  if (length == string.length_uintptr) return string;
  if (length == 0) return kEmptyString;
  if (length == 1) {
    return StringFromSingleCharCode(StringCharCodeAt(string, from));
  }

  try {
    StringToStringSlice(string) otherwise OneByte, TwoByte;
  } label OneByte(p: String, o: intptr, s: ConstSlice<char8>) {
    if (length < kSlicedStringMinLength) {
      return WriteToString(
          s, Convert<intptr>(from),
          AllocateNonEmptySeqOneByteString(Convert<uint32>(length)), length);
    }
    return AllocateSlicedOneByteString(
        Convert<uint32>(length), p, SmiTag(o + Convert<intptr>(from)));
  } label TwoByte(p: String, o: intptr, s: ConstSlice<char16>) {
    if (length < kSlicedStringMinLength) {
      return WriteToString(
          s, Convert<intptr>(from),
          AllocateNonEmptySeqTwoByteString(Convert<uint32>(length)), length);
    }
    return AllocateSlicedTwoByteString(
        Convert<uint32>(length), p, SmiTag(o + Convert<intptr>(from)));
  }
}

builtin SubString(implicit context: Context)(
    s: String, from: Smi, to: Smi): String {
  return SubString(
      s, Unsigned(Convert<intptr>(from)), Unsigned(Convert<intptr>(to)));
}

builtin StringSubstring(implicit context: Context)(
    s: String, from: intptr, to: intptr): String {
  return SubString(s, Unsigned(from), Unsigned(to));
}

// ES6 #sec-string.prototype.substring
transitioning javascript builtin StringPrototypeSubstring(
    js-implicit context: NativeContext, receiver: JSAny)(...arguments): String {
  // Check that {receiver} is coercible to Object and convert it to a String.
  const string: String = ToThisString(receiver, 'String.prototype.substring');
  const length: uintptr = string.length_uintptr;

  // Conversion and bounds-checks for {start}.
  const arg0 = arguments[0];
  let start: uintptr = arg0 != Undefined ? ClampToIndexRange(arg0, length) : 0;

  // Conversion and bounds-checks for {end}.
  const arg1 = arguments[1];
  let end: uintptr =
      arg1 != Undefined ? ClampToIndexRange(arg1, length) : length;
  if (end < start) {
    const tmp: uintptr = end;
    end = start;
    start = tmp;
  }
  return SubString(string, start, end);
}
}
